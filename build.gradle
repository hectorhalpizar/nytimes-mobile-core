plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.5.10'
    id 'com.android.library'
    id 'kotlin-android-extensions'
}

def gitCommitNumber = {
    try {
        return 'git rev-list --count --first-parent HEAD'.execute().text.trim().toInteger().intValue()
    } catch (Exception e) {
        return 0
    }
}

group = "me.hectorhalpizar.lib"
version = "1.0"

repositories {
    google()
    jcenter()
    mavenCentral()
}

kotlin {
    android()
    iosX64('ios') {
        binaries {
            framework {
                baseName = 'library'
            }
        }
    }
    sourceSets {
        commonMain {

        }
        commonTest {
            dependencies {
                implementation kotlin('test')
            }
        }
        androidMain {
            dependencies {
                implementation 'com.google.android.material:material:1.2.1'
            }
        }
        androidTest {
            dependencies {
                implementation 'junit:junit:4.13'
            }
        }
        iosMain {

        }
        iosTest {

        }
    }
}

android {
    compileSdkVersion 31
    sourceSets.main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
    defaultConfig {
        minSdkVersion 24
        targetSdkVersion 31
        versionCode gitCommitNumber()
        versionName "$version.$versionCode"
        setProperty("archivesBaseName", "$group.$archivesBaseName-$versionName")
    }
    buildTypes {
        release {
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'src/androidMain/proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}