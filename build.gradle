plugins {
    id 'org.jetbrains.kotlin.multiplatform' version '1.5.10'
    id 'com.android.library'
    id 'kotlin-android-extensions'
    id 'maven-publish'
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties"))) //Set env variable GPR_USER & GPR_API_KEY if not adding a properties file

def gitCommitNumber = {
    try {
        return 'git rev-list --count --first-parent HEAD'.execute().text.trim().toInteger().intValue()
    } catch (Exception ignored) {
        return 0
    }
}

def version_major = 1
def version_minor = 0
def version_revision = gitCommitNumber()
def version_code_tag = "draft"

group = "me.hectorhalpizar.lib"
version = "$version_major.$version_minor.$version_revision-$version_code_tag"

repositories {
    google()
    jcenter()
    mavenCentral()
}

kotlin {
    android {
        publishLibraryVariantsGroupedByFlavor = true
        publishLibraryVariants("debug", "release")
    }
    iosX64('ios') {
        binaries {
            framework {
                baseName = 'library'
            }
        }
    }
    sourceSets {
        commonMain {

        }
        commonTest {
            dependencies {
                implementation kotlin('test')
            }
        }
        androidMain {
            dependencies {
                implementation 'com.google.android.material:material:1.2.1'
            }
        }
        androidTest {
            dependencies {
                implementation 'junit:junit:4.13'
            }
        }
        iosMain {

        }
        iosTest {

        }
    }
}

android {
    compileSdkVersion 31
    sourceSets.main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 31
        versionCode version_revision
        versionName "$version"
        setProperty("archivesBaseName", "$group.$archivesBaseName")
    }
    buildTypes {
        release {
            debuggable false
            // Enables code shrinking, obfuscation, and optimization for only
            // your project's release build type.
            minifyEnabled true

            // Includes the default ProGuard rules files that are packaged with
            // the Android Gradle plugin. To learn more, go to the section about
            // R8 configuration files.
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'src/androidMain/proguard-rules.pro'
        }

        debug {
            debuggable true
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

publishing {
    repositories {
        maven {
            name = "nytimes-mobile-core"
            url = uri("https://maven.pkg.github.com/hectorhalpizar/nytimes-mobile-core")
            credentials {
                username = githubProperties['gpr.user'] ?: System.getenv("USERNAME")
                password = githubProperties['gpr.key'] ?: System.getenv("TOKEN")

                println("gpr.user ${githubProperties['gpr.user']}")
                println("gpr.key ${githubProperties['gpr.key']}")
            }
        }
    }
}